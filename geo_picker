#!/usr/bin/env perl
use Mojolicious::Lite -signatures, -async;
use DDP;

use constant {
  OSM_API => 'https://overpass-api.de/api/interpreter',
};

helper query_osm => sub ($c, $geom) {

  # coordinates is giving as [ long, lat ]
  my @coordinates = @{$geom->{geometry}->{coordinates}->[0]};
  my @sorted
    = sort { $a->[0] <=> $b->[0] and $a->[1] <=> $b->[1] } @coordinates;
  my $bottom = shift @sorted;
  my $upper  = pop @sorted;

  # osm query requires [ lat, long ]
  my $str   = join(',', reverse(@$bottom), reverse(@$upper));
  my $query = <<EOQ;
[out:json];
node
  [amenity=school]
  ($str);
out;
EOQ

  return $c->app->ua->post_p(OSM_API() => {Accept => '*/*'} => form => {data => $query});

};

helper settings => sub( $c ) {

  # TODO: loop through routes and set path to their route names
  return {
    path => {
      url  => $c->url_for('root')->to_abs,
      bbox => $c->url_for('bbox')->to_abs,
    },
  };
};

get '/' => sub ( $c ) {
  $c->render(template => 'index', settings => $c->settings);
} => 'root';

post 'geo/bbox' => sub ( $c ) {
  $c->app->log->debug('Receiving bound box');
  $c->render_later;

  # TODO: subprocess the bound box
  my $geojson = $c->req->json;
  my $q       = $c->query_osm($geojson);

  $q->then( sub ($tx) {
    $c->render( json => $tx->result->json );
  })->catch( sub ($err) {
    warn "Failed: $err";
    $c->render( text => $err, code => 503 );
  });
} => 'bbox';

app->start;

__DATA__

@@ index.html.ep
% use Mojo::JSON qw(to_json);
<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset='utf-8'>
		<meta name='viewport' content='width=device-width,initial-scale=1'>

		<title>Svelte app</title>
		<link rel='icon' type='image/png' href='/favicon.png'>
		<link rel='stylesheet' href='/global.css'>
		<link rel='stylesheet' href='/build/bundle.css'>

        <script>window.__app_settings = <%== to_json $settings %> </script>
		<script defer src='/build/bundle.js'></script>
	</head>

	<body>
	</body>
</html>
